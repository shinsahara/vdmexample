\section {回帰テストケース}
\subsection {責任}
「運賃を得る」クラス、及び関連するクラスをテストする。

\begin{vdm_al}
class TestCaseT  is subclass of TestCase, 鉄道ネットデータ

values
public v最大値 = 1E8;

instance variables
--public s路線検索 : 路線検索 := new ダイクストラ算法による路線検索(v駅集合, v路線集合, v路線単位集合);
public s路線検索 : 路線検索 := new ダイクストラ算法による路線検索(v駅集合, v路線単位集合);
public s運賃を得る : 運賃を得る := 
	new 運賃を得る([
			mk_運賃表辞書`行(0,1,150),
			mk_運賃表辞書`行(1,3,160),
			mk_運賃表辞書`行(3,6,190),
			mk_運賃表辞書`行(6,10,220),
			mk_運賃表辞書`行(10,15,250),
			mk_運賃表辞書`行(15,v最大値,300)
			],
			v駅集合,
			--v路線集合,
			v路線単位集合);

operations
public print : seq of char ==> ()
print(s) == 
	let - = new IO().echo(s) in skip;

end TestCaseT

class TestCaseT0001 is subclass of TestCaseT
operations
-- constructor
public TestCaseT0001: seq of char ==> TestCaseT0001
TestCaseT0001(name) == setName(name);

public test01: () ==> ()
test01 () == (
	def wDistance = 
			s路線検索.最短距離(v東京, v新宿)
	in
	assertTrue("\t test01 計算結果が間違っている\n",
		wDistance = 7.7 and
  		s運賃を得る.適用する(v東京, v新宿) = 220
	)
);

end TestCaseT0001

class TestCaseT0002 is subclass of TestCaseT
operations
-- constructor
public TestCaseT0002: seq of char ==> TestCaseT0002
TestCaseT0002(name) == setName(name);

public test01: () ==> ()
test01 () == (
	def wDistance = 
			s路線検索.最短距離(v四ツ谷, v品川)
	in
	assertTrue("\t test01 計算結果が間違っている。\n",	
		wDistance = 9.5 and
  		s運賃を得る.適用する(v四ツ谷, v品川) = 220
	)
);

end TestCaseT0002

class TestCaseT0003 is subclass of TestCaseT
operations
-- constructor
public TestCaseT0003: seq of char ==> TestCaseT0003
TestCaseT0003(name) == setName(name);

public test01: () ==> ()
test01 () == (
	trap <RuntimeError> with
		print("\t test01 期待した事前条件エラーを検出した。\n") in (
	def - = 
		s路線検索.最短距離(v東京, v東京) 
	in
		print("\t test01 予想外のエラーに遭遇。\n")
	)
);

end TestCaseT0003

class TestCaseT0004 is subclass of TestCaseT
operations
-- constructor
public TestCaseT0004: seq of char ==> TestCaseT0004
TestCaseT0004(name) == setName(name);

public test01: () ==> ()
test01 () == (
	trap <RuntimeError> with
		print("\t test01 期待した事前条件エラーを検出した。\n") in (
	def - = 
		s路線検索.最短距離(v東京, vコペンハーゲン) 
	in
		print("\t test01 予想外のエラーに遭遇。\n")
	)
);

end TestCaseT0004
\end{vdm_al}