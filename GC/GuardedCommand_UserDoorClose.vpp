\section{ガードコマンド\_利用者\_ドアを閉める}
利用者がドアを閉めるガードコマンド。

\begin{vdm_al}
class ガードコマンド_利用者_ドアを閉める is subclass of ガードコマンド_利用者

operations
public ガードコマンド_利用者_ドアを閉める : 場所グラフ`場所ID ==> ガードコマンド_利用者_ドアを閉める
ガードコマンド_利用者_ドアを閉める(a場所ID) == (
	s場所ID := a場所ID
);
\end{vdm_al}

\subsection{ガードコマンド\_利用者\_ドアを閉める、のcheck}
移動先へのドアが解錠されているならば、ドアを閉める。

ドアを開けた結果と、利用者が前にいた場所を設定した結果は、局所黒板の更新黒板属性に設定する。

\begin{vdm_al}
public check : 黒板 ==> ()
check (a大域黒板) == (
	def mk_(w黒板属性DB, w更新黒板属性DB) = a大域黒板.得る() in
	s局所黒板.全属性を設定する(w黒板属性DB, w更新黒板属性DB);
	def mk_(w黒板属性DB, -) = s局所黒板.得る();
			w利用者のいる場所DB = w黒板属性DB(mk_token("実際の利用者居場所"));
			w利用者の居場所ID = s場所ID;
			w利用者集合 = dom (w利用者のいる場所DB :> {w利用者の居場所ID}) --この場所(w利用者の居場所ID)にいる利用者の集合
	in (
	if w利用者集合 = {} then return;
	def w利用者 = 利用者`到着時間が一番早い利用者を選ぶ(w利用者集合)
	in (
	if w利用者.移動していない() then return;
	def
		w利用者の異動元場所 = w利用者.前にいた場所を得る();
		wドアDB : map ドア`ドア位置 to ドア`ドア型 = w黒板属性DB(mk_token("ドア"))
	in (
	if  w利用者の異動元場所 = w利用者の居場所ID then return;
	def
		w閉めるべきドア =  wドアDB(mk_(w利用者の異動元場所, w利用者の居場所ID))
	in
	if w閉めるべきドア.開いている() then (
		dcl w更新閉めるべきドア : ドア := w閉めるべきドア.コピーする(mk_(w利用者の異動元場所, w利用者の居場所ID));
		dcl w更新利用者 : 利用者 := w利用者.コピーする("ガードコマンド_利用者_ドアを閉める");
		w更新閉めるべきドア.閉める();
		w更新閉めるべきドア.施錠する();
		w更新利用者.前にいた場所を設定する(nil);
		s局所黒板.更新黒板属性を設定する(mk_token("ドア"), {mk_(w利用者の異動元場所, w利用者の居場所ID) |-> w更新閉めるべきドア});
		s局所黒板.更新黒板属性を設定する(mk_token("実際の利用者居場所"), {w更新利用者 |-> w利用者の居場所ID});
		def - = debug >= 5 => new IO().echo(
			"\n利用者が、ドアを閉めました。" ^
			VDMUtil`val2seq_of_char[利用者 * 場所グラフ`場所ID * 場所グラフ`場所ID]
				(mk_(w利用者, w利用者の異動元場所, w利用者の居場所ID)) ^
			"\n")
		in skip
	) -- if
	))) -- def in
);
\end{vdm_al}

\subsection{ガードコマンド\_利用者\_ドアを閉める、のupdate}
局所黒板の利用者の居る場所DBとドアDBを大域黒板に設定する。

\begin{vdm_al}
public update : 黒板 ==> ()
update(a大域黒板) == 
	def w更新黒板属性DB = s局所黒板.更新黒板属性を得る();
		w更新ドアDB = w更新黒板属性DB(mk_token("ドア"));
		w更新利用者のいる場所DB = w更新黒板属性DB(mk_token("実際の利用者居場所"))
	in (
	a大域黒板.設定する(mk_token("ドア"), w更新ドアDB);
	a大域黒板.設定する(mk_token("実際の利用者居場所"), w更新利用者のいる場所DB);
	skip
);

end ガードコマンド_利用者_ドアを閉める
\end{vdm_al}

\begin{rtinfo}
{vdm.tc}[ガードコマンド_利用者_ドアを閉める]
\end{rtinfo}