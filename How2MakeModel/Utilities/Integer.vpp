/*	1
2	-------------------------------------------------------------------------------		
3	- Copyright (c) 2005, Shin Sahara		
4	- 		
5	- All rights reserved.		
6	- 		
7	- Redistribution and use in source and binary forms,		
8	- with or without modification, are permitted provided that		
9	- the following conditions are met:		
10	- 		
11	- * Redistributions of source code must retain the above copyright notice,		
12	-   this list of conditions and the following disclaimer.		
13	- * Redistributions in binary form must reproduce the above copyright notice,		
14	-   this list of conditions and the following disclaimer in the documentation		
15	-   and/or other materials provided with the distribution.		
16	- * Neither the name of the SCSK CORPORATION nor the names of		
17	-   its contributors may be used to endorse or promote products derived from		
18	-   this software without specific prior written permission.		
19	- 		
20	- THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS		
21	- "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT		
22	- LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR		
23	- A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR		
24	- CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,		
25	- EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,		
26	- PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR		
27	- PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF		
28	- LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING		
29	- NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS		
30	- SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.		
31	-------------------------------------------------------------------------------		
32	*/

class Integer

/*
 責任
   整数共通の振る舞いを表す。
 概要
   すべての整数に共通な機能を定義する。
 */
	
functions 

--変換関数群
		
--整数を数字文字列に変換する
static public asString: int -> seq of char 
asString(a整数) == VDMUtil`val2seq_of_char[int](a整数);

--整数をZZZ9.ZZ形式の数字文字列に変換するスタブ関数（まだ小数点は考慮していない）
static public asStringZ: seq of char -> int -> seq1 of char 
asStringZ(変換指定)(整数) == 
	let	マイナス記号 = '-'	in
	if 整数 < 0 then
		if 変換指定(1) = マイナス記号 then
			[マイナス記号] ^ asStringZ補助関数(String`subStr(変換指定,2,len 変換指定))(-整数, true)
		else
			asStringZ補助関数(変換指定)(-整数, true)
	else
		if 変換指定(1) = マイナス記号 then
			asStringZ補助関数(String`subStr(変換指定,2,len 変換指定))(整数, true)
		else
			asStringZ補助関数(変換指定)(整数, true) ;
 		
static public asStringZ補助関数: seq of char -> nat  * bool -> seq1 of char 
asStringZ補助関数(変換指定)(自然数, これまでゼロだった) == 
  	let	変換指定文字列長 = len 変換指定,
  		変換指定文字 = 変換指定(変換指定文字列長),
  		変換指定文字列 = String`subStr(変換指定,1,変換指定文字列長 - 1),
  		余り = 自然数 mod 10,
  		商 = 自然数 div 10,
  		ゼロだった = 余り = 0 and これまでゼロだった and 商 <> 0 --１桁で0の場合は、変換指定がzなら空白を返したい
  	in
  		cases 変換指定文字列:
  			[]		-> asCharZ(変換指定文字)(余り, ゼロだった),
  			others	-> 
  				asStringZ補助関数(変換指定文字列)(商, ゼロだった) ^ 
  				asCharZ(変換指定文字)(余り, ゼロだった)
 		end;

values
数字列 = "0123456789";

functions
static public asCharZ : char -> nat * bool ->  seq1 of char | bool
asCharZ(変換指定文字)(a自然数, これまでゼロだった) ==
	if a自然数 = 0 then
		if 変換指定文字 in set {'z', 'Z'} and これまでゼロだった then
			"0"
		elseif 変換指定文字 = '0'  or 変換指定文字 = '9' then
			"0"
		else
			" "	--変換指定文字の全部のケースは考慮していない
	else
		asChar(a自然数);

--整数を文字列の文字に変換する。
static public asChar : nat -> seq1 of char | bool
asChar(a自然数) ==
	if a自然数 >= 10 then 
		false
	else
		[数字列(a自然数+1)];

--最大公約数を得る
static public 最大公約数 : nat -> nat -> nat
最大公約数(x)(y) == if y = 0 then x else 最大公約数(y)(x rem y);

--最小公倍数を得る
static public 最小公倍数 : nat -> nat -> nat
最小公倍数(x)(y) ==
	cases mk_(x, y) :
	mk_(-, 0)	-> 0,
	mk_(0, -)	-> 0,
	mk_(z, w)	-> (z / 最大公約数(x)(y)) * w
	end;
			
end Integer
